ext.globalProjectName = 'minitel'

buildscript {
    // Versions used across modules
    ext.versions = [:]
    ext.versions.with {
        gradleVersionsPlugin = '0.17.0'

        kotlin = '1.2.21'
        rxJava = '2.1.7'
        rxKotlin = '2.2.0'
        jcommander = '1.71'
        libticker = '1.0.0'
        slf4j = '1.7.25'
        jsoup = '1.11.2'
        apacheCommonsText = '1.2'
    }

    repositories {
        mavenLocal()
        google()
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "com.github.ben-manes:gradle-versions-plugin:$versions.gradleVersionsPlugin"
    }
}

allprojects {
    group 'org.jraf'
    version '1.0.0'

    repositories {
        mavenLocal()
        google()
        jcenter()
        mavenCentral()
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.5'
    distributionType = Wrapper.DistributionType.ALL
}

// Run './gradlew dependencyUpdates' to see new versions of dependencies
apply plugin: 'com.github.ben-manes.versions'
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { selection ->
            boolean rejected = ['alpha', 'beta', 'rc'].any { qualifier ->
                selection.candidate.version.toLowerCase() ==~ /.*-${qualifier}.*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}
